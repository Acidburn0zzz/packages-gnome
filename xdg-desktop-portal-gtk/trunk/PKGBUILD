# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Patrick Griffis <tingping@tingping.se>

pkgname=xdg-desktop-portal-gtk
pkgver=1.5.0
pkgrel=1
pkgdesc="A GTK+ backend for xdg-desktop-portal"
url="https://github.com/flatpak/xdg-desktop-portal-gtk"
arch=(x86_64)
license=(LGPL2.1)
depends=(gtk3)
makedepends=(xdg-desktop-portal python git evince)
optdepends=("evince: Print preview")
provides=(xdg-desktop-portal-impl)
_commit=ffbbf4ef155f904e697acb7c33c0fe691a5fa49e  # tags/1.5.0^0
source=("git+https://github.com/flatpak/xdg-desktop-portal-gtk#commit=$_commit"
        0001-configure-Add-missing-dep-on-libX11.patch)
sha256sums=('SKIP'
            '1b855ddbe3aebfa8a21340bab6804b0f244d1885e5197f1d49df4fbd0b4b1bd0')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  git apply -3 ../0001-configure-Add-missing-dep-on-libX11.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --libexecdir=/usr/lib
  make 
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" make install
}
